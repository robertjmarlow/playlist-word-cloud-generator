plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'jacoco'
}

group = 'com.marlowsoft'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

ext {
	set('springShellVersion', "3.2.2")
}

dependencies {
	def immutablesVersion = "2.10.1"
	annotationProcessor "org.immutables:value:$immutablesVersion"
	compileOnly "org.immutables:value:$immutablesVersion"
	implementation 'commons-io:commons-io:2.15.1'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.google.guava:guava:33.0.0-jre'
	implementation 'com.kennycason:kumo-core:1.28'
	implementation 'org.springframework:spring-web'
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.shell:spring-shell-starter'
	implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
	implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
	implementation 'org.jsoup:jsoup:1.17.2'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.shell:spring-shell-dependencies:${springShellVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	java {
		googleJavaFormat()
	}
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}
